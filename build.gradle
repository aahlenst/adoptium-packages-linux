import org.apache.tools.ant.filters.ReplaceTokens

import java.time.OffsetDateTime
import java.time.ZoneOffset
import java.time.format.DateTimeFormatter

plugins {
	id "java"
}

ext {
	junitVersion = "5.6.2"
	testcontainersVersion = "1.14.3"
	assertjCoreVersion = "3.16.1"
}

repositories {
	mavenCentral()
}

group "org.adoptium"
version "1.0.0-SNAPSHOT"

def buildType = "openjdk-jdk"
def majorVersion = 11

/*
 * The versions list defines the Debian and Ubuntu versions that this package is uploaded to.
 *
 * If a version like sid is missing here, the package won't be included in the sid repository and therefore not
 * installable via apt. Never use suite names like testing or unstable. This list is usually bigger than the list
 * of versions we test with as defined by the class DebianFlavours in the source set packageTest.
 *
 * **Attention**: If you alter the list, check if the class DebianFlavours needs to be updated, too.
 */
def deb_versions = ["stretch", "buster", "bullseye", "xenial", "bionic", "focal", "groovy"]

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
	targetCompatibility = JavaVersion.VERSION_1_8
}

sourceSets {
	packageTest {
		compileClasspath += sourceSets.main.output
		runtimeClasspath += sourceSets.main.output
	}
}

configurations {
	packageTestImplementation.extendsFrom implementation
	packageTestRuntimeOnly.extendsFrom runtimeOnly
}

dependencies {
	packageTestImplementation "org.junit.jupiter:junit-jupiter:$junitVersion"
	packageTestImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
	packageTestImplementation "org.testcontainers:junit-jupiter:$testcontainersVersion"
	packageTestImplementation "org.assertj:assertj-core:$assertjCoreVersion"
}

test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

task deb {
	dependsOn "assemble"

	group = "packaging"
	description = "Creates deb package for Debian flavours."

	def outputDir = new File(project.buildDir.absolutePath, "deb")
	outputs.dir(outputDir)

	doLast {
		def datePattern = DateTimeFormatter.ofPattern("E, dd MMM yyyy HH:mm:ss xx").withLocale(Locale.US)
		project.copy {
			from("$projectDir/incoming/OpenJDK11U-jdk_x64_linux_hotspot_11.0.8_10.tar.gz")
			into("${buildDir}/generated/packaging")
		}
		project.copy {
			from("src/main/packaging/$majorVersion/$buildType") {
				filter(ReplaceTokens, tokens: [
					"buildversion": "15+35-202008102051",
					"pkgdate"     : OffsetDateTime.now(ZoneOffset.UTC).format(datePattern),
					"pkgversion"  : "11.0.8+10-0~adopt0",
					"tarball"     : "OpenJDK11U-jdk_x64_linux_hotspot_11.0.8_10.tar.gz"
				])
			}
			into("${buildDir}/generated/packaging")
		}
		project.exec {
			workingDir getRootDir()
			commandLine "docker", "build",
				"-t", "adoptium-packages-linux-build-deb",
				"-f", "deb.Dockerfile",
				getProjectDir().absolutePath
		}

		project.exec {
			workingDir getRootDir()
			commandLine "docker", "run", "--rm",
				"--mount", "type=bind,source=${buildDir},target=/home/builder/workspace",
				"--mount", "type=bind,source=${outputDir.absolutePath},target=/home/builder/out",
				"-e", "VERSIONS=${String.join(" ", deb_versions)}",
				"adoptium-packages-linux-build-deb:latest"
		}
	}
}

tasks.register("package") {
	dependsOn "deb"

	group = "packaging"
	description = "Creates Linux packages."
}

task packageTest(type: Test) {
	dependsOn "package"

	description = 'Tests the generated packages.'
	group = 'verification'

	testClassesDirs = sourceSets.packageTest.output.classesDirs
	classpath = sourceSets.packageTest.runtimeClasspath

	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}

check.dependsOn(packageTest)
